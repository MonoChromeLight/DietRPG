buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"
    defaultConfig {
        applicationId "edu.odu.cs.zomp.dietapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        manifestPlaceholders = [api_key_crashlytics:FABRIC_API_KEY]
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        buildTypes.each {
            it.buildConfigField 'String', 'YUMMLY_APP_KEY', YUMMLY_API_KEY
            it.buildConfigField 'String', 'YUMMLY_APP_SECRET', YUMMLY_API_SECRET
            it.resValue 'string', 'fabric_api_key', FABRIC_API_KEY
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibVersion = '27.0.2'
    butterknifeVersion = '8.8.1'
    retrofit2Version = '2.3.0'
    okHttpVersion = '3.8.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android support libs
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"

    // Firebase
    implementation 'com.google.firebase:firebase-auth:11.4.2'
    implementation 'com.firebaseui:firebase-ui:3.1.0'

    // Networking
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation("com.squareup.retrofit2:retrofit:$retrofit2Version") { exclude module: 'okhttp' }
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2Version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2Version"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Butterknife
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // Image libs
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.3.1'

    // Miscellaneous
    implementation 'com.stepstone.stepper:material-stepper:4.3.0'
    implementation 'me.zhanghai.android.materialratingbar:library:1.2.0'

    // Testing libs
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1',
            { exclude group: 'com.android.support', module: 'support-annotations' })
    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') { transitive = true }
}

apply plugin: 'com.google.gms.google-services'
